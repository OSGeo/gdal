set(BRUNSLI_NAME BRUNSLI)

find_path(BRUNSLI_INCLUDE_DIR brunsli/encode.h
    HINTS ${BRUNSLI_ROOT}
    PATH_SUFFIXES ${BRUNSLI_NAME}/include include
)

find_library(BRUNSLI_ENC_LIB 
    NAMES brunslienc-c
    HINTS ${BRUNSLI_ROOT}
    PATH_SUFFIXES ${BRUNSLI_NAME}/lib lib
)

find_library(BRUNSLI_DEC_LIB 
    NAMES brunslidec-c
    HINTS ${BRUNSLI_ROOT}
    PATH_SUFFIXES ${BRUNSLI_NAME}/lib lib
)

set(BRUNSLI_LIBRARIES ${BRUNSLI_ENC_LIB} ${BRUNSLI_DEC_LIB})

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(BRUNSLI
    REQUIRED_VARS   BRUNSLI_LIBRARIES BRUNSLI_INCLUDE_DIR
)
mark_as_advanced(BRUNSLI_INCLUDE_DIR BRUNSLI_LIBRARIES)

if(BRUNSLI_FOUND)
    if(NOT TARGET BRUNSLI::ENCODE)
        add_library(BRUNSLI::ENCODE UNKNOWN IMPORTED)
        set_target_properties(BRUNSLI::ENCODE PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES ${BRUNSLI_INCLUDE_DIR}
            IMPORTED_LINK_INTERFACE_LANGUAGES C
            IMPORTED_LOCATION ${BRUNSLI_ENC_LIB}
        )
    endif()
    if(NOT TARGET BRUNSLI::DECODE)
        add_library(BRUNSLI::DECODE UNKNOWN IMPORTED)
        set_target_properties(BRUNSLI::DECODE PROPERTIES
            INTERFACE_INCLUDE_DIRECTORIES ${BRUNSLI_INCLUDE_DIR}
            IMPORTED_LINK_INTERFACE_LANGUAGES C
            IMPORTED_LOCATION ${BRUNSLI_DEC_LIB}
        )
    endif()
endif()
